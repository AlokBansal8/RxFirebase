apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

install {
  repositories.mavenInstaller {
    pom {
      project {
        packaging POM_PACKAGING
        groupId GROUP
        artifactId POM_ARTIFACT_ID

        name POM_NAME
        description POM_DESCRIPTION
        url POM_URL

        licenses {
          license {
            name POM_LICENCE_NAME
            url POM_LICENCE_URL
          }
        }

        scm {
          connection POM_SCM_CONNECTION
          developerConnection POM_SCM_DEV_CONNECTION
          url POM_SCM_URL
        }

        developers {
          developer {
            id POM_DEVELOPER_ID
            name POM_DEVELOPER_NAME
            email POM_DEVELOPER_EMAIL
          }
        }
      }
    }
  }
}

task androidJavadocs(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

  if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
      tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
      }
    }
  }
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
  classifier = 'javadoc'
  from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.sourceFiles
}

artifacts {
  archives androidSourcesJar
  archives androidJavadocsJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
  user = properties.getProperty("bintray.user")
  key = properties.getProperty("bintray.apikey")

  configurations = ['archives']

  dryRun = true //Whether to run this as dry-run, without deploying
  publish = false //If version should be auto published after an upload

  pkg {
    repo = 'maven'
    name = POM_NAME
    licenses = ["Apache-2.0"]
    vcsUrl = POM_SCM_CONNECTION

    version {
      desc = POM_VERSION
      released = new Date()
    }
  }
}